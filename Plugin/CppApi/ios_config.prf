# this file is modified from the Qt 5.8.0 file
# mkspecs/features/mac/sdk.prf
# it is for generating univeral (multi-arch)
# build configurations with Qt 5.6.2

QMAKE_APPLE_DEVICE_ARCHS = armv7 arm64
QMAKE_APPLE_SIMULATOR_ARCHS = i386 x86_64

defineReplace(xcodeSDKInfo) {
    info = $$1
    sdk = $$2
    isEmpty(sdk): \
        sdk = $$QMAKE_MAC_SDK

    isEmpty(QMAKE_MAC_SDK.$${sdk}.$${info}) {
        QMAKE_MAC_SDK.$${sdk}.$${info} = $$system("/usr/bin/xcodebuild -sdk $$sdk -version $$info 2>/dev/null")
        isEmpty(QMAKE_MAC_SDK.$${sdk}.$${info}): error("Could not resolve SDK $$info for \'$$sdk\'")
        cache(QMAKE_MAC_SDK.$${sdk}.$${info}, set stash, QMAKE_MAC_SDK.$${sdk}.$${info})
    }

    return($$eval(QMAKE_MAC_SDK.$${sdk}.$${info}))
}

QMAKE_MAC_SDK_PATH = $$xcodeSDKInfo(Path)
QMAKE_MAC_SDK_PLATFORM_PATH = $$xcodeSDKInfo(PlatformPath)
QMAKE_MAC_SDK_VERSION = $$xcodeSDKInfo(SDKVersion)

sysrootified =
for(val, QMAKE_INCDIR_OPENGL): sysrootified += $${QMAKE_MAC_SDK_PATH}$$val
QMAKE_INCDIR_OPENGL = $$sysrootified

QMAKESPEC_NAME = $$basename(QMAKESPEC)

ios: deployment_target = $$QMAKE_IOS_DEPLOYMENT_TARGET

device_archs = $$QMAKE_APPLE_DEVICE_ARCHS
simulator_archs = $$QMAKE_APPLE_SIMULATOR_ARCHS
archs = $$device_archs $$simulator_archs

isEmpty(archs): \
    error("QMAKE_APPLE_DEVICE_ARCHS or QMAKE_APPLE_SIMULATOR_ARCHS must contain at least one architecture")

QMAKE_XARCH_CFLAGS =
QMAKE_XARCH_LFLAGS =
QMAKE_EXTRA_VARIABLES += QMAKE_XARCH_CFLAGS QMAKE_XARCH_LFLAGS

single_arch {
    device_archs = $$first(device_archs)
    simulator_archs = $$first(simulator_archs)
    archs = $$first(archs)
}

for(arch, archs) {
    contains(simulator_archs, $$arch) {
        sdk = iphonesimulator
        version_identifier = ios-simulator
    } else {
        sdk = iphoneos
        version_identifier = iphoneos
    }

    version_min_flags = \
        -Xarch_$${arch} \
        -m$${version_identifier}-version-min=$$deployment_target
    QMAKE_XARCH_CFLAGS_$${arch} = $$version_min_flags \
        -Xarch_$${arch} \
        -isysroot$$xcodeSDKInfo(Path, $$sdk)
    QMAKE_XARCH_LFLAGS_$${arch} = $$version_min_flags \
        -Xarch_$${arch} \
        -Wl,-syslibroot,$$xcodeSDKInfo(Path, $$sdk)

    QMAKE_XARCH_CFLAGS += $(EXPORT_QMAKE_XARCH_CFLAGS_$${arch})
    QMAKE_XARCH_LFLAGS += $(EXPORT_QMAKE_XARCH_CFLAGS_$${arch})

    QMAKE_EXTRA_VARIABLES += \
        QMAKE_XARCH_CFLAGS_$${arch} \
        QMAKE_XARCH_LFLAGS_$${arch}
}

load(sdk) # force the Qt sdk.prf to load now so we can override some values
QMAKE_CXXFLAGS -= isysroot $$QMAKE_MAC_SDK_PATH
QMAKE_CXXFLAGS += $$QMAKE_XARCH_CFLAGS -arch i386 -arch x86_64
